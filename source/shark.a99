*********************************************************************
*
* Flying Shark
* For the TI-99/4A home computer
*
* August 2017
* Rasmus Moustgaard <rasmus.moustgaard@gmail.com>
*
*********************************************************************

       def  main

**
* Memory mapped addresses
vdpwd  equ  >8c00                      ; VDP write data
vdpwa  equ  >8c02                      ; VDP set read/write address
vdprd  equ  >8800                      ; VDP read data
vdpsta equ  >8802                      ; VDP status
sound  equ  >8400

**
* VDP memory map
ptrntb equ  >0000                      ; Pattern table base
sprptb equ  >1800                      ; Sprite pattern table base
colrtb equ  >2000                      ; Color table base
nametb equ  >3800                      ; Name table base 1
spratb equ  >3b00                      ; Sprite attribute table base

**
* Constants
scr_width:
       equ  32                         ; Width of screen in characters
scr_height:
       equ  24                         ; Height of screen in characters
win_width:
       equ  18                         ; Width of scrolling window in characters
win_height:
       equ  24                         ; Height of scrolling window in characters
map_width:
       equ  18                         ; Map width
map_height:
       equ  358                        ; Map height

**
* Scratch pad
*
* The interrupt handler might update the timer at >8379 or the VDP status byte at >837B.
*
rndsd  equ  >83c0                      ; Random number seed
pad    equ  >8300
wrksp  equ  pad                        ; Workspace
r0lb   equ  wrksp+1                    ; R0 low byte required for VDP routines
r1lb   equ  wrksp+3
r2lb   equ  wrksp+5
r3lb   equ  wrksp+7
r4lb   equ  wrksp+9
r5lb   equ  wrksp+11
r6lb   equ  wrksp+13
r7lb   equ  wrksp+15
r8lb   equ  wrksp+17
r9lb   equ  wrksp+19
plyrws equ  >8320
stack  equ  >8340
padvar equ  stack+>10
* PAD variables
scrly:
       equ  padvar
diff_pointer:
       equ  scrly+2

********************************************************************************
*
* Main program
*
       aorg >a000

main:
       limi 0                          ; Interrupts off
       lwpi wrksp                      ; Setup workspace
       li   r10, stack                 ; Setup stack pointer
       bl   @gmode                     ; Setup graphics mode
       bl   @init                      ; Init
loop   bl   @vsync
       bl   @update_char_list
       bl   @upload_characters
       bl   @draw_screen
       jmp  loop

*********************************************************************
*
* Wait for vertical retrace (CRU)
*
vsync:
       movb @vdpsta, r0
       clr  r12
vsync1:
       tb   2                          ; Test CRU bit for VDP interrupt
       jeq  vsync1
       movb @vdpsta, r0
       b    *r11
*// vsync

*********************************************************************************
*
* Update character list
*
update_char_list:
       mov  @diff_pointer,r0
       clr  r1
       movb *r0+,r2                    ; Add counter
       srl  r2,8
update_char_list_1:
       movb *r0+,r1                    ; Char number
       srl  r1,7                       ; Char offset
       movb *r0+,r3                    ; Global char number MSB
       movb *r0+,@r3lb                 ; Global char number LSB
       sla  r3,4                       ; 16 bytes per pattern/color
       ai   r3,>6000                   ; Add cartridge base address
       mov  r3,@char_list(r1)          ; Store in character list
       dec  r2
       jne  update_char_list_1
*      Return
       b    *r11
*// update_char_list

*********************************************************************************
*
* Upload characters
*
upload_characters:
       mov  r11, *r10+                  ; Push return address onto the stack
       li   r15,vdpwd
       li   r0,ptrntb
       li   r2,128
       li   r3,char_list
upload_characters_1:
       mov  *r3+,r4
       jlt  upload_characters_2
       bl   @vwad
       seto @>6008
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       ai   r0,>2000
       bl   @vwad
       ai   r4,-8
       seto @>600a
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       movb *r4+,*r15
       ai   r0,->2000
upload_characters_2:
       ai   r0,8
       dec  r2
       jne  upload_characters_1
*      Return
       dect r10                        ; Pop return address off the stack
       mov  *r10, r11
       b    *r11
*// upload_characters

*********************************************************************************
*
* Draw a screen
*
draw_screen:
       mov  r11, *r10+                  ; Push return address onto the stack
       li   r15,vdpwd
       li   r0,nametb+((scr_width-win_width)/2)
       li   r1,map_width*(map_height-scr_height)+>6000
       li   r2,scr_height
       seto @>600c
draw_screen_1:
       li   r3,map_width
       bl   @vwad
draw_screen_2:
       movb *r1+,*r15
       dec  r3
       jne  draw_screen_2
       ai   r0,scr_width
       dec  r2
       jne  draw_screen_1
*      Return
       dect r10                        ; Pop return address off the stack
       mov  *r10, r11
       b    *r11
*// draw_screen

*********************************************************************************
*
* Init
*
init:
       li   r0,char_list
       seto r1
       li   r2,128
init1:
       mov  r1,*r0+
       dec  r2
       jne  init1
       clr   @scrly
       li   r0,level_1_000_add
       mov  r0,@diff_pointer
*      Return
       b    *r11
*// init_scrbuf

*********************************************************************************
*
* Set graphics mode
*
gmode:
       mov  r11, *r10+                  ; Push return address onto the stack
       clr  r0
       li   r1, vregs
       li   r2, 8
gmode1:
       movb *r1+, @r0lb
       bl   @vwtr                      ; Set register
       ai   r0, >0100
       dec  r2
       jne  gmode1
*      Patterns
       seto @>6008
       li   r0, ptrntb
       clr  r1
       li   r2, >800
       bl   @vdpwr
*      Colors
       seto @>600A
       li   r0, colrtb
       clr  r1
       li   r2, >800
       bl   @vdpwr
*      Name table
       li   r0, nametb
       li   r1, >ff00
       li   r2, >300
       bl   @vdpwr
*      Sprite attributes
       li   r0, spratb
       li   r1, >d000
       bl   @vsbw
*      Return
       dect r10                        ; Pop return address off the stack
       mov  *r10, r11
       b    *r11
*      VDP registers
vregs:
       byte >02                        ; Graphics II mode
       byte >e2                        ; 16K, display on, interrupt enabled
       byte nametb/>400                ; Name table
       byte >9f                        ; Color table
       byte ptrntb/>800+>00            ; Pattern table
       byte spratb/>80                 ; Sprite attribute table
       byte sprptb/>800                ; Sprite pattern table
       byte >01                        ; Backdrop color
*// gmode

*********************************************************************
*
* VDP Write To Register
*
* R0 MSB: VDP register to write to
* R0 LSB: Value to write
*
vwtr:
       swpb r0
       movb r0, @vdpwa                  ; Send low byte (value) to write to VDP register
       swpb r0
       ori  r0, >8000                   ; Set up a VDP register write operation (10)
       movb r0, @vdpwa                  ; Send high byte (address) of VDP register
       b    *r11
*// vwtr

*********************************************************************
*
* VDP Set Write Address
*
* R0: Write address in VDP RAM
*
* R0 is modified, but can be restored with: ANDI R0,>3FFF
*
vwad:
       swpb r0
       movb r0, @vdpwa                  ; Send low byte of VDP RAM write address
       swpb r0
       ori  r0, >4000                   ; Set read/write bits 14 and 15 to write (01)
       movb r0, @vdpwa                  ; Send high byte of VDP RAM write address
       b    *r11
*// vwad

*********************************************************************
*
* VDP Single Byte Read
*
* R0   Read address in VDP RAM
* R1   MSB of R1 set to byte from VDP RAM
*
vsbr:
       movb @r0lb, @vdpwa               ; Send low byte of VDP RAM write address
       movb r0, @vdpwa                  ; Send high byte of VDP RAM write address
       movb @vdprd, r1                  ; Read byte from VDP RAM
       b    *r11
*// vsbr

*********************************************************************
*
* VDP Single Byte Write
*
* R0: Write address in VDP RAM
* R1: MSB of R1 sent to VDP RAM
*
* R0 is modified, but can be restored with: ANDI R0,>3FFF
*
vsbw:
       swpb r0
       movb r0, @vdpwa                  ; Send low byte of VDP RAM write address
       swpb r0
       ori  r0, >4000                   ; Set read/write bits 14 and 15 to write (01)
       movb r0, @vdpwa                  ; Send high byte of VDP RAM write address
       movb r1, @vdpwd                  ; Write byte to VDP RAM
       b    *r11
*// vsbw

***************************************************************************
*
* VDP Multiple Byte Read
*
* R0: Starting read address in VDP RAM
* R1: Starting write address in CPU RAM
* R2: Number of bytes to read from VDP RAM
*
* Modifies R0-R2
*
vmbr:
       swpb r0
       movb r0, @vdpwa                  ; Send low byte of VDP RAM write address
       swpb r0
       movb r0, @vdpwa                  ; Send high byte of VDP RAM write address
vmbr0:
       li   r0, vdprd
vmbr1:
       movb *r0, *r1+                   ; Read byte from VDP RAM
       dec  r2                         ; Byte counter
       jne  vmbr1                      ; Check if finished
       b    *r11
*// vmbr

*********************************************************************
*
* Fast CPU to VDP copy, replaces VMBW
*
* R0: Destination address
* R1: Source address
* R2: Number of bytes to copy
*
vdpcp:
       mov  r11, *r10+                  ; Push return address onto the stack
       swpb r0
       movb r0, @vdpwa                  ; Send low byte of VDP RAM write address
       swpb r0
       ori  r0, >4000                   ; Set the two MSbits to 01 for write
       movb r0, @vdpwa                  ; Send high byte of VDP RAM write address
       li   r0, vdpwd
vdpcp0:
       mov  r2, r3
       srl  r3, 3                       ; Number of groups of 8
       jeq  vdpcp2
vdpcp1:
       movb *r1+, *r0
       movb *r1+, *r0
       movb *r1+, *r0
       movb *r1+, *r0
       movb *r1+, *r0
       movb *r1+, *r0
       movb *r1+, *r0
       movb *r1+, *r0
       dec  r3
       jne  vdpcp1
       andi r2, >0007                   ; Isolate number of remaining bytes
       jeq  vdpcp3
vdpcp2:
       movb *r1+, *r0
       dec  r2
       jne  vdpcp2
vdpcp3:
       dect r10                        ; Pop return address off the stack
       mov  *r10, r11
       b    *r11
*// vdpcp

*********************************************************************
*
* Fast CPU to VDP write, replaces VSMW
*
* R0: Destination address
* R1: Byte to write in MSB
* R2: Number of bytes to copy
*
vdpwr:
       swpb r0
       movb r0, @vdpwa                  ; Send low byte of VDP RAM write address
       swpb r0
       ori  r0, >4000                   ; Set the two MSbits to 01 for write
       movb r0, @vdpwa                  ; Send high byte of VDP RAM write address
       li   r0, vdpwd
       mov  r2, r3
       srl  r3, 3                       ; Number of groups of 8
       jeq  vdpwr2
vdpwr1:
       movb r1, *r0
       movb r1, *r0
       movb r1, *r0
       movb r1, *r0
       movb r1, *r0
       movb r1, *r0
       movb r1, *r0
       movb r1, *r0
       dec  r3
       jne  vdpwr1
       andi r2, >0007                   ; Isolate number of remaining bytes
       jeq  vdpwr3
vdpwr2:
       movb r1, *r0
       dec  r2
       jne  vdpwr2
vdpwr3:
       b    *r11
*// vdpwr

*********************************************************************
*
* Data
*
char_list:
       bss  128*2

       copy "level1-diff.a99"

       end  main
